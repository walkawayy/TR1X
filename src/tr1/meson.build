project(
  'TR1X',
  'c',
  default_options: [
    'c_std=c2x',
    'warning_level=3',
  ],
  meson_version: '>=1.3.0',
)

staticdeps = get_option('staticdeps')

trx = subproject('libtrx', default_options: {
  'tr_version': '1',
  'staticdeps': staticdeps,
})
c_compiler = meson.get_compiler('c')

fs = import('fs')
relative_dir = fs.relative_to(meson.current_source_dir(), meson.global_build_root())
build_opts = [
  # common options
  '-Wno-unused',
  '-Wno-unused-parameter',
  '-Wno-microsoft-anon-tag',
  '-Wno-gnu-binary-literal',
  '-Wno-gnu-empty-initializer',
  '-Wno-gnu-zero-variadic-macro-arguments',
  '-DMESON_BUILD',
  '-fms-extensions',
  '-fno-omit-frame-pointer',
  # end of common options
  '-ffile-prefix-map=@0@/='.format(relative_dir),
  '-DTR_VERSION=1',
] + trx.get_variable('defines')

add_project_arguments(build_opts, language: 'c')

# Always dynamically link on macOS
if host_machine.system() == 'darwin'
  staticdeps = false
endif

null_dep = dependency('', required: false)

dep_trx = trx.get_variable('dep_trx')
dep_avcodec = dependency('libavcodec', static: staticdeps)
dep_avformat = dependency('libavformat', static: staticdeps)
dep_avutil = dependency('libavutil', static: staticdeps)
dep_mathlibrary = c_compiler.find_library('m', static: staticdeps, required : false)
dep_swscale = dependency('libswscale', static: staticdeps)
dep_swresample = dependency('libswresample', static: staticdeps)
dep_sdl2 = dependency('SDL2', static: staticdeps)
dep_zlib = null_dep

if not staticdeps
  dep_zlib = dependency('zlib', static: staticdeps)
endif

# autogenerated files
resources = []
python3 = find_program('python3', required: true)
git = find_program('git', required: true)

init = custom_target(
  'fake_init',
  output: ['init.c'],
  command: [python3, meson.project_source_root() + '/../../tools/tr1/generate_init', '-o', meson.current_build_dir() / '@OUTPUT0@'],
  build_always_stale: true,
)

version_rc = custom_target(
  'fake_version',
  output: ['version.rc'],
  command: [python3, meson.project_source_root() + '/../../tools/tr1/generate_rcfile', '-o', '@OUTPUT0@'],
  build_always_stale: true,
)

icon_rc = custom_target(
  'fake_icon',
  output: ['icon.rc'],
  command: [python3, meson.project_source_root() + '/../../tools/tr1/generate_rcfile', '-o', '@OUTPUT0@'],
)

link_args = []

if host_machine.system() == 'windows'
  windows = import('windows')

  resources = [
    windows.compile_resources(version_rc),
    windows.compile_resources(icon_rc),
  ]

  link_args += ['-static']
endif

sources = [
  init,
  'game/box.c',
  'game/camera/common.c',
  'game/camera/photo_mode.c',
  'game/carrier.c',
  'game/clock.c',
  'game/collide.c',
  'game/console/cmd/easy_config.c',
  'game/console/common.c',
  'game/console/setup.c',
  'game/creature.c',
  'game/effects.c',
  'game/fmv.c',
  'game/game/game.c',
  'game/game/game_draw.c',
  'game/game_string.c',
  'game/gameflow.c',
  'game/gun/gun.c',
  'game/gun/gun_misc.c',
  'game/gun/gun_pistols.c',
  'game/gun/gun_rifle.c',
  'game/inject.c',
  'game/input.c',
  'game/interpolation.c',
  'game/inventory.c',
  'game/inventory_ring/control.c',
  'game/inventory_ring/draw.c',
  'game/inventory_ring/vars.c',
  'game/item_actions.c',
  'game/item_actions/bubbles.c',
  'game/item_actions/chain_block.c',
  'game/item_actions/earthquake.c',
  'game/item_actions/explosion.c',
  'game/item_actions/finish_level.c',
  'game/item_actions/flicker.c',
  'game/item_actions/flipmap.c',
  'game/item_actions/flood.c',
  'game/item_actions/floor_shake.c',
  'game/item_actions/lara_effects.c',
  'game/item_actions/powerup.c',
  'game/item_actions/raising_block.c',
  'game/item_actions/sand.c',
  'game/item_actions/stairs2slope.c',
  'game/item_actions/turn_180.c',
  'game/items.c',
  'game/lara/cheat.c',
  'game/lara/col.c',
  'game/lara/common.c',
  'game/lara/control.c',
  'game/lara/draw.c',
  'game/lara/hair.c',
  'game/lara/look.c',
  'game/lara/misc.c',
  'game/lara/state.c',
  'game/level.c',
  'game/los.c',
  'game/lot.c',
  'game/music.c',
  'game/objects/common.c',
  'game/objects/creatures/ape.c',
  'game/objects/creatures/bacon_lara.c',
  'game/objects/creatures/baldy.c',
  'game/objects/creatures/bat.c',
  'game/objects/creatures/bear.c',
  'game/objects/creatures/centaur.c',
  'game/objects/creatures/cowboy.c',
  'game/objects/creatures/crocodile.c',
  'game/objects/creatures/cutscene_player.c',
  'game/objects/creatures/larson.c',
  'game/objects/creatures/lion.c',
  'game/objects/creatures/mummy.c',
  'game/objects/creatures/mutant.c',
  'game/objects/creatures/natla.c',
  'game/objects/creatures/pierre.c',
  'game/objects/creatures/pod.c',
  'game/objects/creatures/raptor.c',
  'game/objects/creatures/rat.c',
  'game/objects/creatures/skate_kid.c',
  'game/objects/creatures/statue.c',
  'game/objects/creatures/torso.c',
  'game/objects/creatures/trex.c',
  'game/objects/creatures/wolf.c',
  'game/objects/effects/blood.c',
  'game/objects/effects/body_part.c',
  'game/objects/effects/bubble.c',
  'game/objects/effects/dart_effect.c',
  'game/objects/effects/ember.c',
  'game/objects/effects/explosion.c',
  'game/objects/effects/flame.c',
  'game/objects/effects/gunshot.c',
  'game/objects/effects/missile.c',
  'game/objects/effects/natla_gun.c',
  'game/objects/effects/pickup_aid.c',
  'game/objects/effects/ricochet.c',
  'game/objects/effects/splash.c',
  'game/objects/effects/twinkle.c',
  'game/objects/general/boat.c',
  'game/objects/general/bridge_common.c',
  'game/objects/general/bridge_flat.c',
  'game/objects/general/bridge_tilt1.c',
  'game/objects/general/bridge_tilt2.c',
  'game/objects/general/cabin.c',
  'game/objects/general/camera_target.c',
  'game/objects/general/cog.c',
  'game/objects/general/door.c',
  'game/objects/general/drawbridge.c',
  'game/objects/general/earthquake.c',
  'game/objects/general/keyhole.c',
  'game/objects/general/moving_bar.c',
  'game/objects/general/pickup.c',
  'game/objects/general/puzzle_hole.c',
  'game/objects/general/save_crystal.c',
  'game/objects/general/scion1.c',
  'game/objects/general/scion2.c',
  'game/objects/general/scion3.c',
  'game/objects/general/scion4.c',
  'game/objects/general/scion_holder.c',
  'game/objects/general/switch.c',
  'game/objects/general/trapdoor.c',
  'game/objects/general/waterfall.c',
  'game/objects/setup.c',
  'game/objects/traps/damocles_sword.c',
  'game/objects/traps/dart.c',
  'game/objects/traps/dart_emitter.c',
  'game/objects/traps/ember_emitter.c',
  'game/objects/traps/falling_block.c',
  'game/objects/traps/falling_ceiling.c',
  'game/objects/traps/flame_emitter.c',
  'game/objects/traps/lava_wedge.c',
  'game/objects/traps/lightning_emitter.c',
  'game/objects/traps/midas_touch.c',
  'game/objects/traps/movable_block.c',
  'game/objects/traps/pendulum.c',
  'game/objects/traps/rolling_ball.c',
  'game/objects/traps/sliding_pillar.c',
  'game/objects/traps/spikes.c',
  'game/objects/traps/teeth_trap.c',
  'game/objects/traps/thors_hammer_handle.c',
  'game/objects/traps/thors_hammer_head.c',
  'game/objects/vars.c',
  'game/option/option.c',
  'game/option/option_compass.c',
  'game/option/option_controls.c',
  'game/option/option_controls_pick.c',
  'game/option/option_examine.c',
  'game/option/option_graphics.c',
  'game/option/option_passport.c',
  'game/option/option_sound.c',
  'game/output.c',
  'game/overlay.c',
  'game/packer.c',
  'game/phase/phase.c',
  'game/phase/phase_cutscene.c',
  'game/phase/phase_demo.c',
  'game/phase/phase_game.c',
  'game/phase/phase_inventory.c',
  'game/phase/phase_photo_mode.c',
  'game/phase/phase_stats.c',
  'game/requester.c',
  'game/room.c',
  'game/room_draw.c',
  'game/savegame/savegame.c',
  'game/savegame/savegame_bson.c',
  'game/savegame/savegame_legacy.c',
  'game/screen.c',
  'game/shell.c',
  'game/sound.c',
  'game/spawn.c',
  'game/stats/common.c',
  'game/text.c',
  'game/ui/common.c',
  'game/ui/widgets/stats_dialog.c',
  'game/ui/widgets/paginator.c',
  'game/ui/widgets/photo_mode.c',
  'game/viewport.c',
  'global/enum_map.c',
  'global/vars.c',
  'math/matrix.c',
  'specific/s_output.c',
  'specific/s_shell.c',
  resources,
]

dependencies = [
  dep_trx,
  dep_avcodec,
  dep_avformat,
  dep_avutil,
  dep_mathlibrary,
  dep_sdl2,
  dep_swresample,
  dep_swscale,
  dep_zlib,
]

executable(
  'TR1X',
  sources,
  dependencies: dependencies,
  link_args: link_args,
  win_subsystem: 'windows',
  install: true,
)

if host_machine.system() == 'darwin'
  install_subdir('../../data/tr1/ship/cfg', install_dir : 'Contents/Resources')
  install_subdir('../../data/tr1/ship/data', install_dir : 'Contents/Resources')
  install_subdir('../../data/tr1/ship/shaders', install_dir : 'Contents/Resources')
  install_data('../../data/tr1/mac/icon.icns', install_dir : 'Contents/Resources')
  install_data('../../data/tr1/mac/Info.plist', install_dir : 'Contents')
  meson.add_install_script('../../tools/tr1/mac/bundle_dylibs')
endif
